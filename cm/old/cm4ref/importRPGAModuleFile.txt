  `Procedure pImportRPGACode
  `Tim WHite
  `5/5/98
  `20:31
  `This procedure imports the RPGA code file for a convention

C_STRING(80;$ConCode;$ConName;$Coordinator;$DayPhone;$HomePhone;$EmailAddr;$City;$State)
C_STRING(80;$WebSite;$RPGAType;$EventName;$ScenarioCd;$NbrRounds;$RuleCode;$RuleDesc)
C_TEXT($Description)
C_DATE($StartDate;$EndDate)
C_INTEGER($Num;$Max)
C_BOOLEAN($FirstRow;$HeaderRow)

vImpFile:=Open document("")
If (OK=1)
  $HeaderRow:=True
  $FirstRow:=True
  READ WRITE([Event])
  Repeat 
    RECEIVE PACKET(vImpFile;$ConCode;<>TB)
    RECEIVE PACKET(vImpFile;$ConName;<>TB)
    RECEIVE PACKET(vImpFile;$StartDate;<>TB)
    RECEIVE PACKET(vImpFile;$EndDate;<>TB)
    RECEIVE PACKET(vImpFile;$Coordinator;<>TB)
    RECEIVE PACKET(vImpFile;$DayPhone;<>TB)
    RECEIVE PACKET(vImpFile;$HomePhone;<>TB)
    RECEIVE PACKET(vImpFile;$EmailAddr;<>TB)
    RECEIVE PACKET(vImpFile;$City;<>TB)
    RECEIVE PACKET(vImpFile;$State;<>TB)
    RECEIVE PACKET(vImpFile;$WebSite;<>TB)
    RECEIVE PACKET(vImpFile;$RPGAType;<>TB)
    RECEIVE PACKET(vImpFile;$EventName;<>TB)
    RECEIVE PACKET(vImpFile;$ScenarioCd;<>TB)
    RECEIVE PACKET(vImpFile;$Description;<>TB)
    RECEIVE PACKET(vImpFile;$NbrRounds;<>TB)
    RECEIVE PACKET(vImpFile;$RuleCode;<>TB)
    RECEIVE PACKET(vImpFile;$RuleDesc;<>CR)
    If ($HeaderRow=False)
      If ($FirstRow=True)
        READ WRITE([Convention])
        ALL RECORDS([Convention])
        FIRST RECORD([Convention])
        [Convention]Email Address:=$ConCode
        [Convention]Name:=$ConName
        [Convention]Sponsor:=$Coordinator
        [Convention]Phone Number:=$HomePhone
        [Convention]Venue:=$City+", "+$State
        SAVE RECORD([Convention])
        UNLOAD RECORD([Convention])
        READ ONLY([Convention])
        $FirstRow:=False
        ALL RECORDS([Event])
        If (Records in selection([Event]) # 0)
          $EvNum:=Max([Event]Code)
          $EvNum:=$EvNum+1
        Else 
          $EvNum:=1
        End if 
      End if 
      If ($ConCode # "")  `Blank Row      
        CREATE RECORD([Event])
        [Event]Code:=$EvNum
        [Event]Event SubType Code:=True
        [Event]Event SubType Code:=$RPGAType
        [Event]System:=$RuleDesc
        [Event]Scenario:=$Description
        [Event]RPGA Event Code:=$ScenarioCd
        [Event]Round Count:=Num($NbrRounds)
        [Event]Sponsor:="the RPGA"
        
        If (Find in array(<>aRPGATypes;$RPGAType)=-1)  `If this RPGA Event Type doesn't yet exist.
          INSERT ELEMENT(<>aRPGATypes;1)  `Fix the array
          <>aRPGATypes{1}:=$RPGAType
          READ WRITE([Event Subtype])  `Fix the table
          CREATE RECORD([Event Subtype])
          [Event Subtype]Code:=$RPGAType
          [Event Subtype]Name:=$EventName
          SAVE RECORD([Event Subtype])
          READ ONLY([Event Subtype])
        End if 
      End if 
      
      pCalcEventName   `Build the complete Event Name      
      SAVE RECORD([Event])
      $EvNum:=$EvNum+1
    Else 
      $HeaderRow:=False
    End if 
  Until (OK=0)  `EOF makes OK=0
  CLOSE DOCUMENT(vImpFile)
End if 
