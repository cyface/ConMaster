  `pExportEvPM
  `Tim White
  `4-9-95
  `0858
  `This procedure exports the current selection of the events file to a
  `« text file, 
  `marking each field with a style tag in brackets <Stylename>
  `This allows importation into a page layout program.

C_STRING(80;$RPGADesc)
C_LONGINT($numRecs)

vExpFile:=Create document("")
If (OK=1)
  FIRST RECORD([Event])
  $numRecs:=Records in selection([Event])
  For ($i;1;$numRecs)
    RELATE MANY([Event])
    ORDER BY([Section];[Section]Numeric Section)
    If ([Event]Event SubType Code=True)
      $RPGA:="<EventTitle>RPGA "
      $RPGADesc:=" "+fGetRPGADesc ([Event]Event SubType Code)
    Else 
      $RPGA:=("<EventTitle>")
    End if 
    SEND PACKET(vExpFile;pCheckBlank (String([Event]Code);"<EventNumber>"))
    SEND PACKET(vExpFile;pCheckBlank ([Event]System+$RPGADesc;$RPGA))
    SEND PACKET(vExpFile;pCheckBlank ([Event]Scenario;"<EventScenario>"))
    SEND PACKET(vExpFile;pCheckBlank ([Event]Author Name;"<EventAuthor>by "))
    If ([Event]Sponsor="")
      $SponsorBy:=""
    Else 
      $SponsorBy:=("Sponsored by "+[Event]Sponsor+".  ")
    End if 
    
    SEND PACKET(vExpFile;("<EventDescription>"+[Event]Description+"  "+$SponsorBy+[Event]Level+"."+<>CR))
    
    FIRST RECORD([Section])
    numSubRecs:=Records in selection([Section])
    For ($j;1;numSubRecs)
      SEND PACKET(vExpFile;"<EventTime>"+[Section]Full Code+<>TB+fGetDayCode ([Section]Date)+<>TB+fGetTimeCode ([Section]Start Time)+"-"+fGetTimeCode ([Section]End Time)+<>CR)
      SEND PACKET(vExpFile;pCheckBlank ([Section]Location;"<EventTime>"+<>TB))
      NEXT RECORD([Section])
    End for 
    NEXT RECORD([Event])
  End for 
  CLOSE DOCUMENT(vExpFile)
End if 
