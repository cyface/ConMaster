  `Procedure rTopPerson
  `Tim White
  `4/2/98
  `18:10
  `This procedure displays the options dialog
  `The OK button on the dialog calls rTopPerson2
  `to do the calc and display

C_BOOLEAN($Judge)
C_LONGINT($i;$j;$NumEvents;$NumPeople;$LastID;$FirstBlank;$NumBlank)
C_STRING(10;gPlayJudge;$NumPeopleSt)

CenterWindow (240;170;1;"";"CloseWindow")
DIALOG([Convention];"TopDialog")
CLOSE WINDOW
If (zPlayer=1)
  $Judge:=False
  gPlayJudge:="Player"
Else 
  $Judge:=True
  gPlayJudge:="Judge"
End if 

gInMessage:=False

Message2 ("Tabulating Score Information, Please Wait... 1")
READ ONLY([Person Section])
READ ONLY([Person])
READ ONLY([Section])

ALL RECORDS([Person Section])  `start with all records
QUERY([Person Section];[Person Section]Place # 0;*)  `only look at scored packets.
QUERY([Person Section]; & ;[Person Section]Judge=$Judge)  `get just the judges or Players

If (bRPGOnly=1)  `Narrow by event type/RPGA
  QUERY SELECTION([Person Section];[Event]Event Type Name="role@")
  If (bRPGAOnly=1)
    QUERY SELECTION([Person Section];[Event]Event SubType Code=True)
  End if 
End if 

  `This is our base set of records from now on, so save it
CREATE SET([Person Section];"TopPersonSet")

  `Create an array of just the unique person IDs we're dealing with.
ARRAY LONGINT(aTopIDs;0)
RELATE ONE SELECTION([Person Section];[Person])  `Select all the person records for the person section records
SELECTION TO ARRAY([Person]ID;aTopIDs)

  `For each of our people, calculate the required information.

$NumPeople:=Size of array(aTopIDs)
$NumPeopleSt:=String($NumPeople)
ARRAY LONGINT(aTopCounts;$NumPeople)
ARRAY REAL(aTopAvg;$NumPeople)
ARRAY TEXT(aTopScores;$NumPeople)
ARRAY STRING(80;aTopNames;$NumPeople)

For ($i;1;$NumPeople)
  Message2 ("Tabulating Score Information, Please Wait... "+<>CR+"Scanning Record "+String($i)+" of "+$NumPeopleSt)
  USE SET("TopPersonSet")
  QUERY SELECTION([Person Section];[Person Section]Person ID=aTopIDs{$i})
  
  $NumEvents:=Records in selection([Person Section])
  If ($NumEvents)>=Num(vMinEvents))  `only process this person if they have the minimum # of events
    
    aTopCounts{$i}:=$NumEvents
    aTopAvg{$i}:=Round(Average([Person Section]Score);2)
    
    RELATE ONE([Person Section]Person ID)
    aTopNames{$i}:=[Person]Person Name
    
    If ($Judge=True)
      ORDER BY([Person Section];[Person Section]Score;<)
    Else 
      ORDER BY([Person Section];[Person Section]Place;>)
    End if 
    For ($j;1;aTopCounts{$i})
      aTopScores{$i}:=aTopScores{$i}+String([Person Section]Place)+" "
      NEXT RECORD([Person Section])
    End for 
  End if 
End for 

  `Now we delete the blank space in the arrays
SORT ARRAY(aTopCounts;aTopAvg;aTopScores;aTopNames;<)
$FirstBlank:=Find in array(aTopCounts;0)
$NumBlank:=($NumPeople-$FirstBlank)+1
DELETE ELEMENT(aTopCounts;$FirstBlank;$NumBlank)
DELETE ELEMENT(aTopAvg;$FirstBlank;$NumBlank)
DELETE ELEMENT(aTopScores;$FirstBlank;$NumBlank)
DELETE ELEMENT(aTopNames;$FirstBlank;$NumBlank)

Message2Close 


CenterWindow (555;343;5;"Top "+gPlayJudge+" Display";"CloseWindow")
DIALOG([Convention];"TopPerson")
CLOSE WINDOW

  `release the array memory
ARRAY LONGINT(aTopIds;0)
ARRAY LONGINT(aTopCounts;0)
ARRAY REAL(aTopAvg;0)
ARRAY TEXT(aTopScores;0)
ARRAY STRING(80;aTopNames;0)

CLEAR SET("TopPersonSet")
