`Procedure pSaveScrPkt2
  `Tim White
  `11-91-97
  `21:24
  `This procedure commits all the variables on a score packet
  `to the appropriate [Person Section] fields.
  `It also preforms numerous error checks.

  `###DECLARATION SECTION
C_INTEGER($NumRecs;$i;$j)
C_LONGINT(PersonID;$Process;$PlayerTotal)
C_STRING(80;$WhichFld;$WhichFld2;$User;$Machine;$ProcessName;RPGANum)
C_BOOLEAN($ErrorFlag)
C_TEXT($ErrorText)
ARRAY STRING(80;aRegNames;0)
ARRAY LONGINT(aRegIds;0)
  `###END DECLARATION SECTION

  `###SETUP AND HOUSEKEEPING SECTION
  `We won't need to change these files, so make sure they're marked read only
READ ONLY([Person])
READ ONLY([Section])
READ ONLY([Event])
READ WRITE([Person Section])
CREATE EMPTY SET([Person Section];"PacketSet")
$ErrorFlag:=False  `Assume everything's rosy to start
$ErrorText:=""
  `###END SETUP AND HOUSEKEEPING SECTION

  `###PACKET ERROR CHECKING SECTION

  `First we need to figure out how many players there are.
[Score Packet]Num of Players:=0
If (vP1ID#0)
  [Score Packet]Num of Players:=[Score Packet]Num of Players+1
End if 
If (vP2ID#0)
  [Score Packet]Num of Players:=[Score Packet]Num of Players+1
End if 
If (vP3ID#0)
  [Score Packet]Num of Players:=[Score Packet]Num of Players+1
End if 
If (vP4ID#0)
  [Score Packet]Num of Players:=[Score Packet]Num of Players+1
End if 
If (vP5ID#0)
  [Score Packet]Num of Players:=[Score Packet]Num of Players+1
End if 
If (vP6ID#0)
  [Score Packet]Num of Players:=[Score Packet]Num of Players+1
End if 
If (vP7ID#0)
  [Score Packet]Num of Players:=[Score Packet]Num of Players+1
End if 
If (vP8ID#0)
  [Score Packet]Num of Players:=[Score Packet]Num of Players+1
End if 

  `Error Check to make sure that all RPGA packets have RPGA Nums
  `For all players & judge

If ([Event]RPGA=True)
  If (vP0RPGA="")
    $ErrorText:=$ErrorText+*CR+""+"The Judge must have an RPGA Number."
  End if 
  If (vP1RPGA="")
    $ErrorText:=$ErrorText+*CR+""+"Player 1 must have an RPGA Number."
  End if 
  If (vP2RPGA="")
    $ErrorText:=$ErrorText+*CR+""+"Player 2 must have an RPGA Number."
  End if 
  If (vP3RPGA="")
    $ErrorText:=$ErrorText+*CR+""+"Player 3 must have an RPGA Number."
  End if 
  If (vP4RPGA="")
    $ErrorText:=$ErrorText+*CR+""+"Player 4 must have an RPGA Number."
  End if 
  If (vP5RPGA="")
    $ErrorText:=$ErrorText+*CR+""+"Player 5 must have an RPGA Number."
  End if 
  If (vP6RPGA="")
    $ErrorText:=$ErrorText+*CR+""+"Player 6 must have an RPGA Number."
  End if 
  If (vP7RPGA="")
    $ErrorText:=$ErrorText+*CR+""+"Player 7 must have an RPGA Number."
  End if 
  If (vP8RPGA="")
    $ErrorText:=$ErrorText+*CR+""+"Player 8 must have an RPGA Number."
  End if 
End if 

  `Error Check to make sure that all score info was filled in correctly.
Case of 
  : ([Score Packet]Num of Players=1)
    $PlayerTotal:=8
  : ([Score Packet]Num of Players=2)
    $PlayerTotal:=22
  : ([Score Packet]Num of Players=3)
    $PlayerTotal:=39
  : ([Score Packet]Num of Players=4)
    $PlayerTotal:=56
  Else 
    $PlayerTotal:=(([Score Packet]Num of Players*10)+20)
End case 

  `Now we have to tal the player scores
$TotPlayScr:=vTotal1+vTotal2+vTotal3+vTotal4+vTotal5+vTotal6+vTotal7+vTotal8

If ($TotPlayScr#$PlayerTotal)
  $ErrorFlag:=True
  $ErrorText:=$ErrorText+*CR+"The Player Vote total should be "+String($PlayerTotal)
End if 

If ([Score Packet]Group Score=0)
  $ErrorFlag:=True
  $ErrorText:=$ErrorText+*CR+"Group Score is blank."
End if 

If ([Score Packet]NonVoting=True)  `Turn off errors if this is a no-vote packet
  $ErrorFlag:=False
End if 

If ($ErrorFlag=True)  `We must have gotten an error on the packet itself.
  $ErrorText:=$ErrorText+*CR+"You must correct these error(s) before you can save the packet."+*CR+"
"
  Alert2 ($ErrorText;*WARN_ICON)
Else 
    `###END PACKET ERROR CHECKING SECTION
  
    `###WHOLE PACKET DATA SET SECTION
  [Score Packet]Judge ID:=vP0ID
    `###END WHOLE PACKET DATA SET SECTION
  
  Message2 ("Saving Score Packet..."+*CR+"Please Wait...")
  
    `###BEGIN MAIN LOOP, ONE ITERATION PER PERSON ATTACHED TO THE PACKET
    `Go through each person var set and find/create their person section record.
  For ($i;0;8)
      `LOOP SETUP AND HOUSEKEEPING  
    $WhichFld:=String($i)  `need a string version of the loop ctl var
    PersonID:=(Get pointer("vP"+$WhichFld+"ID"))»  `Define the PersonID Variable
    RPGANum:=(Get pointer("vP"+$WhichFld+"RPGA"))»  `Define the RPGANum variable
      `END LOOP SETUP AND HOUSEKEEPING 
    If (PersonID#0)  `If person attachment space is blank, skip it
        `FIND THE [PERSON SECTION] & [PERSON] RECORD FOR THIS ATTACHMENT POINT    
      
      SEARCH([Person Section];[Person Section]Person ID=PersonID;*)
      SEARCH([Person Section]; & ;[Person Section]Complete EvNum=[Score Packet]Complete EvNum;*)
      SEARCH([Person Section]; & ;[Person Section]Place=0)
      $NumRecs:=Records in selection([Person Section])
      SEARCH([Person];[Person]Person ID=PersonID)
      FIRST RECORD([Person])
        `FIND THE [PERSON SECTION] & [PERSON] RECORD FOR THIS ATTACHMENT POINT   
      
        `CHECK OR CREATE [PERSON SECTION] RECORD SECTION
      
      Case of   `Make sure a single person record exists for them, and create or error if not
        : ($NumRecs=0)
            `Person is not currently registered for this event, better register them
          CREATE RECORD([Person Section])
          [Person Section]Person ID:=[Person]Person ID
          [Person Section]Packet ID:=[Score Packet]Packet ID
          [Person Section]Complete EvNum:=[Score Packet]Complete EvNum
          [Person Section]Reg Type:="Score Packet"
          [Person Section]Old CompEvNum:=[Score Packet]Complete EvNum
          $NewRecord:=True
          
        : ($NumRecs>1)
          BugAlert ("pSaveScorePkt procedure, multiple reg's for this event")
          ABORT
          
        Else 
          $NewRecord:=False
      End case 
        `CHECK OR CREATE [PERSON SECTION] RECORD COUNT SECTION
      
      
        `SET [PERSON SECTION] FIELDS SECTION
        `Time to set the [Person Section] Info to correspond to the screen variables
      
      If ($NewRecord=False)
        LOAD RECORD([Person Section])  `Get the record we're going to change
      End if 
      [Person Section]Score:=Get pointer("vTotal"+$WhichFld)»
      [Person Section]Scenario Score:=Get pointer("vP"+$WhichFld+"Scen")»
      
      If ($i=0)  `This is the judge pass
        [Person Section]Judge:=True
        [Person Section]Score:=(vTotal0NPR/[Score Packet]Num of Players)*6  `Proration formula
        [Person Section]Place:=[Person Section]Score
        [Person Section]Judge Score:=vTotal0NPR  `Save Non-prorated score
      Else 
        [Person Section]Judge:=False
        [Person Section]Place:=Get pointer("vPlace"+$WhichFld)»
        [Person Section]Judge Score:=Get pointer("v0"+$WhichFld)»
      End if 
      
      [Person Section]Packet Position:=$i
      [Person Section]Packet ID:=[Score Packet]Packet ID
        `END SET [PERSON SECTION] FIELDS SECTION
      
        `CHANGE [PERSON]RPGA NUMBER SECTION
      If ([Person]RPGA Number#RPGANum)  `Set their RPGA Number to value in field, because the scorer wants to change it
        READ WRITE([Person])
        LOAD RECORD([Person])
        [Person]RPGA Number:=RPGANum
        If (Locked([Person]))  `Multiuser is fun!
          LOCKED ATTRIBUTES([Person];$Process;$User;$Machine;$ProcessName)
          $ErrorFlag:=True
          $ErrorText:=$ErrorText+*CR+"A record needing to be changed is locked by "+$User+"
"+$Machine+" in window "+$ProcessName+".  They will need to save or cancel their window before you can save yours."
        End if 
        SAVE RECORD([Person])
        READ ONLY([Person])
      End if 
        `END CHANGE [PERSON]RPGA NUMBER SECTION
      
        `SAVE [PERSON SECTION] RECORD SECTION
      If (Locked([Person Section]))  `Multiuser is fun!
        LOCKED ATTRIBUTES([Person Section];$Process;$User;$Machine;$ProcessName)
        $ErrorFlag:=True
        $ErrorText:=$ErrorText+*CR+"A record needing to be changed is locked by "+$User+"
"+$Machine+" in window "+$ProcessName+".  They will need to save or cancel their window before you can save yours."
      End if 
      SAVE RECORD([Person Section])
        `END SAVE [PERSON SECTION] RECORD SECTION
      
        `CHECK FOR DUPLICATES SECTION    
        `Added records in set check on 12-14-97 at 18:19 to fix problem with transactions
      If (($NewRecord=False) & (Is in set("PacketSet")))  `We already added this person to the set!
Uh Oh!
        $ErrorFlag:=True
        $ErrorText:=$ErrorText+*CR+"You have one or more duplicate people listed on this score packet."
      Else 
        ADD TO SET([Person Section];"PacketSet")  `This is a good record so we want to keep it  
      End if 
        `CHECK FOR DUPLICATES SECTION       
      
    End if   `End if # 0 
  End for 
    `###END OF MAIN LOOP
  
    `###REMOVE ERRONEOUS [PERSON SECTION] RECORDS SECTION
    `Neutralize erroneous [Person Section] records that might be out there
    `Like someone was on a packet and shouldn't be, etc.
    `This covers us if people switch a person out for another and don't
    `Put the original person back on the packet anywhere
  
  Message2 ("Eliminating Erroneous Records..."+*CR+"Please Wait...")
  SEARCH([Person Section];[Person Section]Packet ID=[Score Packet]Packet ID)
  CREATE SET([Person Section];"AllPacketSet")
  DIFFERENCE("AllPacketSet";"PacketSet";"BadPacketSet")
  If (Records in set("BadPacketSet")>0)
    USE SET("BadPacketSet")
    
    SEARCH SELECTION([Person Section];[Person Section]Reg Type="Score Packet")
    If (Records in selection([Person Section])>0)
      DELETE SELECTION([Person Section])  `Delete records if they were created from a score packet.
      If (Records in set("LockedSet")>0)
        $ErrorFlag:=True
        $ErrorText:=$ErrorText+*CR+"One or more records needing to be changed was locked by another user or window. "+"Close other windows and/or wait a moment and try saving again."
      End if 
    End if 
    
    USE SET("BadPacketSet")
    APPLY TO SELECTION([Person Section];pClearScoreInfo )
    If (Records in set("LockedSet")>0)
      $ErrorFlag:=True
      $ErrorText:=$ErrorText+*CR+"One or more records needing to be changed was locked by another user or window. "+"Close other windows and/or wait a moment and try saving again."
    End if 
  End if 
    `###END REMOVE ERRONEOUS [PERSON SECTION] RECORDS SECTION
  
    `###DISPLAY ERROR MESSAGE OR COMMIT TRANSACTION SECTION
  If ($ErrorFlag=True)
    *PACKETOK:=False
    $ErrorText:="The following error(s) were detected while trying to save this score
packet:"+$ErrorText
    $ErrorText:=$ErrorText+*CR+"You must correct these error(s) before you can save."
    Alert2 ($ErrorText)
  Else 
    *PACKETOK:=True
  End if 
    `###END DISPLAY ERROR MESSAGE OR COMMIT TRANSACTION SECTION
  
    `###CLOSE & CLEANUP SECTION
  CLEAR SET("PacketSet")
  CLEAR SET("BadPacketSet")
  CLEAR SET("AllPacketSet")
  
  Message2Close 
    `###END CLOSE & CLEANUP SECTION
End if 